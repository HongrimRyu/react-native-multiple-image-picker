buildscript {
    ext.kotlin_version = "2.0.0"

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.7.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

def reactNativeArchitectures() {
    def value = rootProject.getProperties().get("reactNativeArchitectures")
    return value ? value.split(",") : ["armeabi-v7a", "x86", "x86_64", "arm64-v8a"]
}

def isNewArchitectureEnabled() {
    return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: 'org.jetbrains.kotlin.android'
apply from: '../nitrogen/generated/android/MultipleImagePicker+autolinking.gradle'

if (isNewArchitectureEnabled()) {
    apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["MultipleImagePicker_" + name]
}

def getExtOrIntegerDefault(name) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["MultipleImagePicker_" + name]).toInteger()
}

android {
    namespace "com.margelo.nitro.multipleimagepicker"

    ndkVersion getExtOrDefault("ndkVersion")
    compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

    defaultConfig {
        minSdkVersion getExtOrIntegerDefault("minSdkVersion")
        targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
        buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()

        externalNativeBuild {
            cmake {
                cppFlags "-O2 -frtti -fexceptions -Wall -fstack-protector-all"
                // 링커 플래그에 max-page-size=16384 추가
                arguments "-DANDROID_STL=c++_shared",
                          "-DCMAKE_SHARED_LINKER_FLAGS=-Wl,-z,max-page-size=16384"

                abiFilters(*reactNativeArchitectures())
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    packagingOptions {
        excludes = ["META-INF",
                    "META-INF/**",
                    "**/libc++_shared.so",
                    "**/libfbjni.so",
                    "**/libjsi.so",
                    "**/libfolly_json.so",
                    "**/libfolly_runtime.so",
                    "**/libglog.so",
                    "**/libhermes.so",
                    "**/libhermes-executor-debug.so",
                    "**/libhermes_executor.so",
                    "**/libreactnative.so",
                    "**/libreactnativejni.so",
                    "**/libturbomodulejsijni.so",
                    "**/libreact_nativemodule_core.so",
                    "**/libjscexecutor.so"]
    }

    buildFeatures {
        buildConfig true
        prefab true
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        disable "GradleCompatible"
    }

compileOptions {
        // [설정] Kotlin 2.x 및 최신 AGP는 Java 17 이상이 필수입니다.
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    // [추가] Kotlin 컴파일 옵션 (JVM 타겟 17 설정)
    kotlinOptions {
        jvmTarget = "17"
    }

    sourceSets {
        main {
            if (isNewArchitectureEnabled()) {
                java.srcDirs += ["${project.buildDir}/generated/source/codegen/java"]
            }
        }
    }
}
repositories {
    mavenCentral()
    google()
}


dependencies {
    // For < 0.71, this will be from the local maven repo
    // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
    //noinspection GradleDynamicVersion

    // Add a dependency on NitroModules
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
    // PictureSelector basic (Necessary)
    implementation 'io.github.lucksiege:pictureselector:v3.11.2'

    // image compress library (Not necessary)
    implementation 'io.github.lucksiege:compress:v3.11.2'

    // uCrop library (Not necessary)
    implementation 'io.github.lucksiege:ucrop:v3.11.2'

    // simple camerax library (Not necessary)
    implementation 'io.github.lucksiege:camerax:v3.11.2'

    // [Fix] For 16KB Page Size Support, CameraX Version 1.4.0+ Force Update
    def camerax_version = "1.4.0" // 최신 안정 버전 사용
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"

    // exoplayer
    implementation "com.google.android.exoplayer:exoplayer:2.19.1"


    implementation "com.facebook.react:react-native:+"

    // Add a dependency on NitroModules
    implementation project(":react-native-nitro-modules")

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

if (isNewArchitectureEnabled()) {
    react {
        jsRootDir = file("../src/")
        libraryName = "MultipleImagePicker"
        codegenJavaPackageName = "com.margelo.nitro.multipleimagepicker"
    }
}

